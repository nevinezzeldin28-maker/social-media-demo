// Facebook Graph API Reference: https://developers.facebook.com/docs/graph-api/

profile = "social-media/publish-post@1.0"
provider = "facebook"

"""
Publish post to Facebook page feed

"""
map PublishPost {
  apiVersion = call GetApiVersion()
  objectId = input.profileId
  media = input.media

  return map error if(!objectId) { 
    title = "profileId field is missing"
    detail = "Missing or invalid profileId, either page or group ID is expected"
  }

  return map error if(media.length === 0 && !input.text && !input.link) {
    title = "Missing text or link"
    detail = "Either text or link fields must be non-empty"
  }

  return map error if(media.length > 0) {
    title = "Images attachment is not yet supported"
    detail = "not implemented"
  }

  call GetPageAccessToken(pageId = objectId, userAccessToken = parameters.accessToken) {
    return map error if (outcome.error) outcome.error
    
    token = outcome.data.pageAccessToken
  }

  set if(!token) {
    token = parameters.accessToken
  }

  call PublishPhotoPost(pageId = objectId, pageAccessToken = token, input = input) if (media.length > 0) {
    map error if (outcome.error) outcome.error
    return map result outcome.data
  }
  // just text / link -> POST "/{apiVersion}/{objectId}/feed"
  call PublishTextPost(pageId = objectId, pageAccessToken = token, input = input) {
    map error if (outcome.error) outcome.error
    return map result outcome.data
  }

}

operation GetPageAccessToken {
  return if(!args.pageId) {}

  apiVersion = call GetApiVersion()

  http GET "/{apiVersion}/{args.pageId}" {
    request {
      query {
        fields = 'access_token',
        access_token = args.userAccessToken
      }
    }

    response 200 "application/json" {
      return {
        pageAccessToken = body.access_token
      }
    }

    response 400 "application/json" {
      fail {
        title = "Bad request"
        detail = body.error.message
      }
    }

    response 401 "application/json" {
      fail {
        title = "Unauthenticated"
        detail = body.error.message
      }
    }

    response 403 "application/json" {
      fail {
        title = "Forbidden"
        detail = body.error.message
      }
    }
  }
}

operation PublishTextPost {
  input = args.input

  apiVersion = call GetApiVersion()

  http POST "/{apiVersion}/{args.pageId}/feed" {
    request {
      query {
        access_token = args.pageAccessToken,
        link = input.link,
        message = input.text,
        fields = "id,permalink_url",
      }
    }

    response 200 "application/json" {
      return {
        postId = body.id
        link = body.permalink_url
      }
    }

    response 400 "application/json" {
      fail {
        title = "Bad request"
        detail = body.error.message
      }
    }

    response 401 "application/json" {
      fail {
        title = "Unauthenticated"
        detail = body.error.message
      }
    }

    response 403 "application/json" {
      fail {
        title = "Forbidden"
        detail = body.error.message
      }
    }
  }
}

operation PublishPhotoPost {
  // TODO:
  // - single image -> https://developers.facebook.com/docs/graph-api/reference/photo#Creating
  // - multiple images -> https://stackoverflow.com/a/39128878
  return true
}

operation GetApiVersion {
  return 'v12.0'
}